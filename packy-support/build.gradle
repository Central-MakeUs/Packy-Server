plugins {
    id 'java'
    id 'jacoco-report-aggregation'
}

group = 'com.dilly'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // multi module
    implementation project(':packy-domain')

    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'com.slack.api:slack-api-client:1.39.0'

    // jacoco
    jacocoAggregation project(':packy-api')
    jacocoAggregation project(':packy-domain')
}

test {
    useJUnitPlatform()
    finalizedBy 'testCodeCoverageReport'
}

processResources.dependsOn('copySecret')
tasks.register('copySecret', Copy) {
    from '../packy-submodule/support'
    include '*.yml'
    into './src/main/resources'
}

testCodeCoverageReport {
    reports {
        html.required = true
        xml.required = true
    }

    def Qdomains = []
    for (qPattern in '**/QA'..'**/QZ') {
        Qdomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            'com/dilly/exception/**',
                            'com/dilly/**/config/**',
                            'com/dilly/**/dto/**',
                            'com/dilly/**/model/**',
                            'com/dilly/global/constant/**',
                            'com/dilly/global/domain/**',
                            'com/dilly/global/formatter/**',
                            'com/dilly/logging/**'
                    ] + Qdomains)
                })
        )
    }
}

bootJar.enabled = false
jar.enabled = true
